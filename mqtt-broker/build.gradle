repositories {
    // http://ftp.byfly.by/pub/apache.org//activemq/5.15.7/apache-activemq-5.15.7-bin.tar.gz
    //https://downloads.arduino.cc/arduino-1.8.7-linux64.tar.xz
    ivy {
        url 'http://ftp.byfly.by/pub'
        layout 'pattern', {
            //This maps to the pattern: [organisation]:[module]:[revision]:[classifier]@[ext]
            artifact '/[organisation]/activemq/[revision]/[module]-[revision]-bin.[ext]'
        }
    }
}

configurations {
    activemq
}

dependencies {
    activemq group: 'apache.org', name: 'apache-activemq', version: '5.15.7', ext: 'tar.gz'
}

task install() {
    doLast {
        final File dist = project.configurations['activemq'].files[0]
        exec {
            commandLine 'tar', '-xzvf', dist.path, '-C', projectDir
        }
    }
}

task upMqtt() {
    doLast {
        exec {
            commandLine './apache-activemq-5.15.7/bin/activemq', 'start'
        }
    }
}

task stopMqtt() {
    doLast {
        exec {
            commandLine './apache-activemq-5.15.7/bin/activemq', 'stop'
        }
    }
}