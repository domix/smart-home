repositories {
    // http://ftp.byfly.by/pub/apache.org//activemq/5.15.7/apache-activemq-5.15.7-bin.tar.gz
    //https://downloads.arduino.cc/arduino-1.8.7-linux64.tar.xz
    ivy {
        url 'http://ftp.byfly.by/pub'
        layout 'pattern', {
            //This maps to the pattern: [organisation]:[module]:[revision]:[classifier]@[ext]
            artifact '/[organisation]/activemq/[revision]/[module]-[revision]-bin.[ext]'
        }
    }
}

configurations {
    activemq
}

dependencies {
    activemq group: 'apache.org', name: 'apache-activemq', version: '5.15.7', ext: 'tar.gz'
}

def activeMQDir = file("$projectDir/apache-activemq-5.15.7")

task install() {
    onlyIf {
        !activeMQDir.exists()
    }

    doLast {
        println "ActiveMQ will be installed..."
        final File dist = project.configurations['activemq'].files[0]
        exec {
            commandLine 'tar', '-xzf', dist.path, '-C', projectDir
        }
    }
}

task upMqtt(dependsOn: install) {
    doLast {
        exec {
            commandLine "${activeMQDir.path}/bin/activemq", 'start'
        }
    }
}

task stopMqtt() {
    doLast {
        exec {
            commandLine "${activeMQDir.path}/bin/activemq", 'stop'
        }
    }
}